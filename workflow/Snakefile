import json
from itertools import chain


include: "definitions.smk"


with open(build_dir / "runs.json", "r") as f:
    runs = json.load(f)

RUN_IDS = sorted(set(chain(*runs.values())))


ruleorder: plot_bayesian_blocks_lightcurve > plot_dl4


include: "rules/run_selection.smk"
include: "rules/create_dl3.smk"
include: "rules/post_dl3.smk"
include: "rules/bayesian_blocks.smk"


irf_plots = [
    build_dir / f"plots/irf/{irf}_Run{run_id}.pdf"
    for irf in ("aeff", "edisp", "gh_cut", "radmax_cut")
    for run_id in RUN_IDS
]

analyses = [
    x.name for x in config_dir.iterdir() if x.name.startswith("analysis") and x.is_dir()
]

dl3_plots = [
    build_dir / f"plots/{plot}/{run_id}.pdf"
    for run_id in RUN_IDS
    for plot in ["theta2"]
] + [build_dir / "plots/theta2/stacked.pdf"]


# observation plots are arguably dl3
dl4_plots = [
    build_dir / f"plots/{analysis}/{plot}.pdf"
    for analysis in analyses
    for plot in [
        "light_curve",
        "flux_points",
        "observation_plots",
        "bayesian_blocks",
        "bayesian_blocks_comparison",
    ]
]


rule all:
    input:
        dl3_plots,
        dl4_plots,
        irf_plots,


localrules:
    link_paths,
